plugins {
  id "com.palantir.docker" version "0.25.0"
  id 'com.palantir.docker-compose' version "0.25.0"
}

ext {
  interlokParentGradle = project.findProperty('interlokParentGradle') ?: 'https://raw.githubusercontent.com/adaptris-labs/interlok-build-parent/master/build.gradle'
  interlokVersion  = '3.10-SNAPSHOT'
  dockerImageName = project.findProperty('dockerImageName') ?: "adaptrislabs/interlok-gcloud-pubsub"
  dockerImageTag = project.findProperty('dockerImageVersion') ?: "latest"
  interlokMavenLocal = project.findProperty('interlokMavenLocal') ?: false
  enableIntegrationTest = project.findProperty('enableIntegrationTest') ?: false
  interlokVerifyEnvironmentProperties = [
    PUBSUB_HOST: "localhost",
    PUBSUB_PORT: "8042"
  ]
}

dockerfileZip.enabled = false

repositories {
  if (interlokMavenLocal) {
    mavenLocal()
  }
}

allprojects {
  apply from: "${interlokParentGradle}"
  if (enableIntegrationTest) {
    apply from: "$rootDir/integration-test.gradle"
  }
}

dependencies {
  interlokRuntime  group: "com.adaptris", name: "interlok-gcloud-pubsub", version: interlokVersion, changing: true
  interlokJavadocs group: "com.adaptris", name: "interlok-gcloud-pubsub", version: interlokVersion, changing: true, classifier: "javadoc", transitive: false
}

docker {
  name dockerImageName
  tags dockerImageTag
  copySpec.into("./build/distribution").from(installDist)
  pull true
  noCache false
}


dockerCompose {
  dockerComposeFile "docker-compose.yaml"
}

docker.dependsOn check, assemble
